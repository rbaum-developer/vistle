if(TECIO_MPI)
    set(DIR tecio/teciompisrc)
else()
    set(DIR tecio/teciosrc)
endif()

if(WIN32)
    set(BaseFlags "/EHsc /MP /wd\"4996\"")
else()
    set(BaseFlags "-fmessage-length=0 -w")
endif()
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${BaseFlags}")
if(TECIO_MPI)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${MPI_CXX_COMPILE_FLAGS}")
endif()

file(GLOB mainFiles "${DIR}/*.cpp" "${DIR}/*.h")
list(REMOVE_ITEM mainFiles "${CMAKE_CURRENT_SOURCE_DIR}/${DIR}/szcombine.cpp")

if(TECIO_SHARED)
    vistle_add_library(vistle_tecio EXPORT ${VISTLE_LIB_TYPE} ${mainFiles})
else()
    add_library(vistle_tecio STATIC ${mainFiles})
endif()
set_target_properties(vistle_tecio PROPERTIES POSITION_INDEPENDENT_CODE TRUE)
set_target_properties(vistle_tecio PROPERTIES LINKER_LANGUAGE CXX)

target_include_directories(vistle_tecio PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/${DIR}> $<INSTALL_INTERFACE:teciosrc>)

target_compile_definitions(vistle_tecio PRIVATE TP_PROJECT_USES_BOOST BOOST_ALL_NO_LIB MAKEARCHIVE NO_THIRD_PARTY_LIBS NO_ASSERTS)
target_compile_definitions(vistle_tecio PRIVATE tecio_EXPORTS)
if(TECIO_SHARED)
    target_compile_definitions(vistle_tecio PRIVATE BUILD_SHARED_LIBRARIES)
endif()
if(WIN32)
    target_compile_definitions(vistle_tecio PRIVATE MSWIN)
else()
    target_compile_definitions(vistle_tecio PRIVATE LINUX LINUX64)
endif()
if(TECIO_MPI)
    target_compile_definitions(vistle_tecio PUBLIC TECIOMPI)
    target_compile_definitions(vistle_tecio PRIVATE OMPI_SKIP_MPICXX MPICH_SKIP_MPICXX MPI_NO_CPPBIND)
    target_link_libraries(vistle_tecio PRIVATE MPI::MPI_CXX)
endif()

if(NOT TECIO_SHARED)
    add_executable(vistle_szcombine "${DIR}/szcombine.cpp")
    set_target_properties(vistle_szcombine PROPERTIES LINKER_LANGUAGE CXX)
    target_link_libraries(vistle_szcombine vistle_tecio)
    if(NOT WIN32)
        target_link_libraries(vistle_szcombine pthread)
    endif()
    if(TECIO_MPI)
        target_link_libraries(vistle_szcombine vistle_tecio MPI::MPI_CXX)
    endif()
endif()
